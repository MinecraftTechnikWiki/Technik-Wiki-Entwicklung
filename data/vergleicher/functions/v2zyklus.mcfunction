# Wenn ein Spieler auf den Auslöser geklickt hat, wird je nach Klick-Wert die Variable 1 oder 2 um eins erhöht oder verringert
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1}] run scoreboard players add VarVergl.2Variable1 PZVergl.2Bool 1
execute if entity @a[distance=..25,scores={PZVergl.2Bool=2}] run scoreboard players remove VarVergl.2Variable1 PZVergl.2Bool 1
execute if entity @a[distance=..25,scores={PZVergl.2Bool=3}] run scoreboard players add VarVergl.2Variable2 PZVergl.2Bool 1
execute if entity @a[distance=..25,scores={PZVergl.2Bool=4}] run scoreboard players remove VarVergl.2Variable2 PZVergl.2Bool 1

# Wenn im Chat auf ein Feld geklickt wurde, besitzt der Spieler mindestens den Wert eins. Die Ergebnisse werden daraufhin als Wert in die Rüstungsständer gespeichert
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1..}] store success score @e[type=minecraft:armor_stand,tag=EtiVergl.2GLEICH,sort=nearest,limit=1] PZVergl.2Bool if score VarVergl.2Variable1 PZVergl.2Bool = VarVergl.2Variable2 PZVergl.2Bool
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1..}] store success score @e[type=minecraft:armor_stand,tag=EtiVergl.2UNGLEICH,sort=nearest,limit=1] PZVergl.2Bool unless score VarVergl.2Variable1 PZVergl.2Bool = VarVergl.2Variable2 PZVergl.2Bool
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1..}] store success score @e[type=minecraft:armor_stand,tag=EtiVergl.2GROESSERGLEICH,sort=nearest,limit=1] PZVergl.2Bool if score VarVergl.2Variable1 PZVergl.2Bool >= VarVergl.2Variable2 PZVergl.2Bool
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1..}] store success score @e[type=minecraft:armor_stand,tag=EtiVergl.2KLEINERGLEICH,sort=nearest,limit=1] PZVergl.2Bool if score VarVergl.2Variable1 PZVergl.2Bool <= VarVergl.2Variable2 PZVergl.2Bool
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1..}] store success score @e[type=minecraft:armor_stand,tag=EtiVergl.2GROESSER,sort=nearest,limit=1] PZVergl.2Bool if score VarVergl.2Variable1 PZVergl.2Bool > VarVergl.2Variable2 PZVergl.2Bool
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1..}] store success score @e[type=minecraft:armor_stand,tag=EtiVergl.2KLEINER,sort=nearest,limit=1] PZVergl.2Bool if score VarVergl.2Variable1 PZVergl.2Bool < VarVergl.2Variable2 PZVergl.2Bool

# Die Rüstungsständer können den Wert ein oder null besitzen. Je nach Wert wird dann der Wahrheitswert als Name für den jeweiligen Rüstungsständer festgesetzt
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1..}] as @e[type=minecraft:armor_stand,tag=EtiVergl.2Alle,scores={PZVergl.2Bool=1}] run data merge entity @s {CustomName:'{"bold":true,"color":"green","text":"wahr"}'}
execute if entity @a[distance=..25,scores={PZVergl.2Bool=1..}] as @e[type=minecraft:armor_stand,tag=EtiVergl.2Alle,scores={PZVergl.2Bool=0}] run data merge entity @s {CustomName:'{"bold":true,"color":"red","text":"falsch"}'}

# Wenn eingeschaltet wird, benötigt das Erzeugen der Rüstungsständer einen Tick, deshalb wird nach einem geprüft. Hat der Spieler nicht den Wert null, so erhält er die Chat-Nachricht. Dort sieht er die Ergebnisse der Vergleiche
execute if entity @e[type=minecraft:armor_stand,tag=EtiVergl.2GLEICH,sort=nearest,limit=1] as @a[distance=..25] unless entity @s[scores={PZVergl.2Bool=0}] run tellraw @s ["Vergleicher.2: ",{"text":"\nvar1 (","bold":true},{"text":"+1","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für die Variable1 +1"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 1"} },{"text":"!","bold":true},{"text":"-1","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für die Variable1 -1"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 2"} },{"text":") == ","bold":true},{"score":{"name":"VarVergl.2Variable1","objective":"PZVergl.2Bool"},"color":"dark_purple","bold":true},{"text":"\nvar2 (","bold":true},{"text":"+1","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für die Variable2 +1"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 3"} },{"text":"!","bold":true},{"text":"-1","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für die Variable2 -1"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 4"} },{"text":") == ","bold":true},{"score":{"name":"VarVergl.2Variable2","objective":"PZVergl.2Bool"},"color":"dark_purple","bold":true},{"text":"\n(var1 == var2) == ","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für den Wahrheitswert (Gleichheit)"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 5"} },{"entity":"@e[type=minecraft:armor_stand,tag=EtiVergl.2GLEICH,sort=nearest,limit=1]","nbt":"CustomName","interpret":true},{"text":"\n(var1 != var2) == ","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für den Wahrheitswert (Ungleichheit)"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 5"} },{"entity":"@e[type=minecraft:armor_stand,tag=EtiVergl.2UNGLEICH,sort=nearest,limit=1]","nbt":"CustomName","interpret":true},{"text":"\n(var1 >= var2) == ","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für den Wahrheitswert (Größergleich)"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 5"} },{"entity":"@e[type=minecraft:armor_stand,tag=EtiVergl.2GROESSERGLEICH,sort=nearest,limit=1]","nbt":"CustomName","interpret":true},{"text":"\n(var1 <= var2) == ","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für den Wahrheitswert (Kleinergleich)"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 5"} },{"entity":"@e[type=minecraft:armor_stand,tag=EtiVergl.2KLEINERGLEICH,sort=nearest,limit=1]","nbt":"CustomName","interpret":true},{"text":"\n(var1 > var2) == ","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für den Wahrheitswert (Größer)"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 5"} },{"entity":"@e[type=minecraft:armor_stand,tag=EtiVergl.2GROESSER,sort=nearest,limit=1]","nbt":"CustomName","interpret":true},{"text":"\n(var1 < var2) == ","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für den Wahrheitswert (Kleiner)"} },"clickEvent":{"action":"run_command","value":"/trigger PZVergl.2Bool set 5"} },{"entity":"@e[type=minecraft:armor_stand,tag=EtiVergl.2KLEINER,sort=nearest,limit=1]","nbt":"CustomName","interpret":true}]

# Spieler die keinen Wert besitzen oder einen über null erhalten die Erlaubnis einen Auslöser zu nutzen und bekommen den Wert null zugewiesen
execute as @a[distance=..25] unless entity @s[scores={PZVergl.2Bool=0}] run scoreboard players enable @s PZVergl.2Bool
execute as @a[distance=..25] unless entity @s[scores={PZVergl.2Bool=0}] run scoreboard players set @s PZVergl.2Bool 0
