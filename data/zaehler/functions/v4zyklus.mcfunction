# Wenn die Wert-Variable angeklickt wurde, wird diese um eins erhöht. Wenn die Variable angeklickt wurde, wird auf sie der Wert der Wert-Variable darauf addiert. Wenn der Countdown angeklickt wurde, wird der Wert der Ergebnis-Variable in die Countdown-Variable gespeichert und anschließend wird dieser Wert in die Bossleiste als maximaler Wert gespeichert.
execute if entity @a[distance=..25,scores={PZZaehl.4Wert=1}] run scoreboard players add VarZaehl.4Variable PZZaehl.4Wert 1
execute if entity @a[distance=..25,scores={PZZaehl.4Wert=2}] run scoreboard players operation VarZaehl.4Ergebnis PZZaehl.4Wert += VarZaehl.4Variable PZZaehl.4Wert
execute if entity @a[distance=..25,scores={PZZaehl.4Wert=3}] store result bossbar zaehler:v4countdown max run scoreboard players operation VarZaehl.4Countdown PZZaehl.4Wert = VarZaehl.4Ergebnis PZZaehl.4Wert

# Wenn die Countdown-Variable über null liegt, wird der Wert der Tick-Variable um eins reduziert, sie wird in der Schnellzugriffsleite ausgegeben, der Wert der Countdown-Variable selbst wird um eins reduziert und anschließend in die Bossleiste übertragen und der Wert der Tick-Variable wird auf 20 gesetzt.
execute if score VarZaehl.4Countdown PZZaehl.4Wert matches 1.. run scoreboard players remove VarZaehl.4Tick PZZaehl.4Wert 1
execute if score VarZaehl.4Countdown PZZaehl.4Wert matches 1.. if score VarZaehl.4Tick PZZaehl.4Wert matches ..-1 run title @a[distance=..25] actionbar ["",{"score":{"name":"VarZaehl.4Countdown","objective":"PZZaehl.4Wert"},"color":"dark_purple","bold":true}]
execute if score VarZaehl.4Countdown PZZaehl.4Wert matches 1.. if score VarZaehl.4Tick PZZaehl.4Wert matches ..-1 store result bossbar zaehler:v4countdown value run scoreboard players remove VarZaehl.4Countdown PZZaehl.4Wert 1
execute if score VarZaehl.4Countdown PZZaehl.4Wert matches 1.. if score VarZaehl.4Tick PZZaehl.4Wert matches ..-1 run scoreboard players set VarZaehl.4Tick PZZaehl.4Wert 20

# Wenn die Ergebnis-Variable exisitiert, werden alle Spieler in einem Umkreis von 25 Metern ausgewählt und geprüft ob sie nicht den Wert null besitzen. Wenn das zutrifft, erhalten sie eine Chat-Nachricht, in der sie die Variablen erhöhen und den Countdown starten können.
execute if score VarZaehl.4Ergebnis PZZaehl.4Wert matches 0.. as @a[distance=..25] unless entity @s[scores={PZZaehl.4Wert=0}] run tellraw @s ["Zähler.4: ",{"text":"\nwert + 1 = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für den Wert"} },"clickEvent":{"action":"run_command","value":"/trigger PZZaehl.4Wert set 1"} },{"score":{"name":"VarZaehl.4Variable","objective":"PZZaehl.4Wert"},"color":"dark_purple","bold":true},{"text":"\nvar + wert = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für die Variable"} },"clickEvent":{"action":"run_command","value":"/trigger PZZaehl.4Wert set 2"} },{"score":{"name":"VarZaehl.4Ergebnis","objective":"PZZaehl.4Wert"},"color":"dark_purple","bold":true},{"text":"\ncountdown(var)","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für den Countdown"} },"clickEvent":{"action":"run_command","value":"/trigger PZZaehl.4Wert set 3"} }]

# Spieler die nicht den Wert null besitzen erhalten die Erlaubnis für den Auslöser und erhalten den Wert null.
execute as @a[distance=..25] unless entity @s[scores={PZZaehl.4Wert=0}] run scoreboard players enable @s PZZaehl.4Wert
execute as @a[distance=..25] unless entity @s[scores={PZZaehl.4Wert=0}] run scoreboard players set @s PZZaehl.4Wert 0
