# Wenn der Spieler mit dem Dorfbewohner Bill redet, wird sein Punktestand für die Nachricht eingestellt
scoreboard players add @a[distance=..3,scores={PZAufS.1Reden=1..}] PZAufS.1Nachr 1
scoreboard players set @a[distance=..3,scores={PZAufS.1Reden=1..,PZAufS.1Nachr=4..}] PZAufS.1Nachr 1

# Wenn der Spieler für Bill die sechs Zäune und das Zauntor hergestellt hat, besitzt er diese in seinem Inventar und auch das Etikett, dass er dafür den Auftrag bekommen hat
execute if entity @s[tag=!EtiAufS.1AuftragAbgeschlossen] run tag @a[distance=..3,tag=EtiAufS.1BillZaunHerstellen,scores={PZAufS.1Reden=1..},nbt={Inventory:[{id:"minecraft:oak_fence"},{id:"minecraft:oak_fence_gate"}]}] add EtiAufS.1BillZaunHergestellt
execute if entity @s[tag=!EtiAufS.1AuftragAbgeschlossen] if entity @a[distance=..3,tag=EtiAufS.1BillZaunHerstellen,tag=EtiAufS.1BillZaunHergestellt,scores={PZAufS.1Reden=1..}] run data merge entity @s {Offers:{Recipes:[{buy:{id:"minecraft:oak_fence",Count:6b},buyB:{id:"minecraft:oak_fence_gate",Count:1b},sell:{id:"minecraft:emerald",Count:1b},maxUses:1,uses:0} ] } }
execute if entity @s[tag=!EtiAufS.1AuftragAbgeschlossen] run tellraw @a[distance=..3,tag=EtiAufS.1BillZaunHergestellt,scores={PZAufS.1Reden=1..}] ["",{"text":"[1/1]","color":"gray"},{"text":" «","color":"gold","bold":true},{"selector":"@s"},{"text":"» ","color":"gold","bold":true},{"text":"Wie ich sehe hast du die Zäune und das Zauntor bei dir, dann gebe ich dir im Handelsmenü deine Belohnung","color":"yellow","bold":false}]
execute if entity @a[distance=..3,tag=EtiAufS.1BillZaunHerstellen,tag=EtiAufS.1BillZaunHergestellt,scores={PZAufS.1Reden=1..}] run tag @s[tag=!EtiAufS.1AuftragAbgeschlossen] add EtiAufS.1AuftragAbgeschlossen

# Bill gibt bei einem Rechtsklickt auf ihn drei Nachrichten nacheinander aus, um dem Spieler zu erklären, was er von ihm verlangt
tellraw @a[distance=..3,tag=!EtiAufS.1BillZaunHergestellt,scores={PZAufS.1Reden=1..,PZAufS.1Nachr=1}] ["",{"text":"[1/3]","color":"gray"},{"text":" «","color":"gold","bold":true},{"selector":"@s"},{"text":"» ","color":"gold","bold":true},{"text":"Hey du, du siehst aus, als ob du","color":"yellow"},{"text":" [sechs Zäune und ein Zauntor] ","color":"green","hoverEvent":{"action":"show_text","value":{"text":"Stelle 6x Zäune und 1x Zauntor her"} } },{"text":"herstellen könntest.","color":"yellow"} ]
tellraw @a[distance=..3,tag=!EtiAufS.1BillZaunHergestellt,scores={PZAufS.1Reden=1..,PZAufS.1Nachr=2}] ["",{"text":"[2/3]","color":"gray"},{"text":" «","color":"gold","bold":true},{"selector":"@s"},{"text":"» ","color":"gold","bold":true},{"text":"Ich glaube ich hatte hier irgendwo doch meine Werkbank stehen. Dort in der Nähe befinden sich dann auch die Materialien für das Herstellen.","color":"yellow","bold":false}]
tellraw @a[distance=..3,tag=!EtiAufS.1BillZaunHergestellt,scores={PZAufS.1Reden=1..,PZAufS.1Nachr=3}] ["",{"text":"[3/3]","color":"gray"},{"text":" «","color":"gold","bold":true},{"selector":"@s"},{"text":"» ","color":"gold","bold":true},{"text":"Was stehst du hier so ahnungslos herum? Du bekommst das doch bestimmt hin.","color":"yellow","bold":false}]

# Falls der Spieler Bill das erste angeklickt hat, wird die Werkbank und die Truhe mit dem Material platziert. Der Spieler erhält dann das Etikett für den Auftrag
execute if entity @a[distance=..3,tag=!EtiAufS.1BillZaunHerstellen,scores={PZAufS.1Reden=1..,PZAufS.1Nachr=1..}] run setblock ~ ~ ~-5 minecraft:crafting_table replace
execute if entity @a[distance=..3,tag=!EtiAufS.1BillZaunHerstellen,scores={PZAufS.1Reden=1..,PZAufS.1Nachr=1..}] run setblock ~ ~ ~-6 minecraft:chest[facing=north]{Items:[{Slot:12b,id:"minecraft:oak_planks",Count:10b},{Slot:13b,id:"minecraft:stick",Count:8b}]} replace
tag @a[distance=..3,tag=!EtiAufS.1BillZaunHerstellen,scores={PZAufS.1Reden=1..,PZAufS.1Nachr=1..}] add EtiAufS.1BillZaunHerstellen
