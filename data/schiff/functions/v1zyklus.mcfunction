# Wenn der Spieler den Chat angeklickt hat und dabei für dne Auslöser den Wert eins bis vier hat, wird damit die entsprechende Richtung eingestellt.
execute as @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] at @s if entity @p[distance=..5,team=TMSchiff.1Kap,scores={PZSchiff.1Wert=1}] run data merge entity @s {CustomName:'{"bold":true,"color":"blue","text":"Norden"}',Rotation:[180.0f,0.0f]}
execute as @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] at @s if entity @p[distance=..5,team=TMSchiff.1Kap,scores={PZSchiff.1Wert=2}] run data merge entity @s {CustomName:'{"bold":true,"color":"gold","text":"Osten"}',Rotation:[270.0f,0.0f]}
execute as @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] at @s if entity @p[distance=..5,team=TMSchiff.1Kap,scores={PZSchiff.1Wert=3}] run data merge entity @s {CustomName:'{"bold":true,"color":"blue","text":"Süden"}',Rotation:[0.0f,0.0f]}
execute as @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] at @s if entity @p[distance=..5,team=TMSchiff.1Kap,scores={PZSchiff.1Wert=4}] run data merge entity @s {CustomName:'{"bold":true,"color":"gold","text":"Westen"}',Rotation:[90.0f,0.0f]}

# Wenn der Spieler im Chat auf die Neiugung geklickt hat, wird der entsprechende Wert die Neigung einstellen.
execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] if entity @p[distance=..5,team=TMSchiff.1Kap,scores={PZSchiff.1Wert=5}] run data merge entity @e[distance=..2,type=minecraft:armor_stand,tag=EtiSchiff.1Neigung,sort=nearest,limit=1] {CustomName:'{"bold":true,"color":"green","text":"Oben"}'}
execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] if entity @p[distance=..5,team=TMSchiff.1Kap,scores={PZSchiff.1Wert=6}] run data merge entity @e[distance=..2,type=minecraft:armor_stand,tag=EtiSchiff.1Neigung,sort=nearest,limit=1] {CustomName:'{"bold":true,"color":"aqua","text":"Geradeaus"}'}
execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] if entity @p[distance=..5,team=TMSchiff.1Kap,scores={PZSchiff.1Wert=7}] run data merge entity @e[distance=..2,type=minecraft:armor_stand,tag=EtiSchiff.1Neigung,sort=nearest,limit=1] {CustomName:'{"bold":true,"color":"red","text":"Unten"}'}

# Falls der Spieler auf die Fahren-Option geklickt hat, wird die entsprechende Schiff-Funktion geladen
execute as @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] at @s if entity @p[distance=..15,team=TMSchiff.1Kap,scores={PZSchiff.1Wert=8}] run function schiff:v1schiff

# Von der Position des Schiffs-Rüstungsständers wird nach einem Piloten gesucht und dieser darf nicht den Wert null besitzen, muss also den Auslöser betätigt haben. Dann erhält er im Chat alle Steuerungsoptionen des Schiffes.
execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] as @p[distance=..5,team=TMSchiff.1Kap] unless entity @s[scores={PZSchiff.1Wert=0}] run tellraw @s ["Schiff.1: ",{"text":"\nrichtung = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für Norden"} },"clickEvent":{"action":"run_command","value":"/trigger PZSchiff.1Wert set 1"} },{"text":"Norden","color":"gray","bold":true},{"text":"\nrichtung = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für Osten"} },"clickEvent":{"action":"run_command","value":"/trigger PZSchiff.1Wert set 2"} },{"text":"Osten","color":"gray","bold":true},{"text":"\nrichtung = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für Süden"} },"clickEvent":{"action":"run_command","value":"/trigger PZSchiff.1Wert set 3"} },{"text":"Süden","color":"gray","bold":true},{"text":"\nrichtung = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für Westen"} },"clickEvent":{"action":"run_command","value":"/trigger PZSchiff.1Wert set 4"} },{"text":"Westen","color":"gray","bold":true},{"text":"\nrichtung == ","bold":true},{"entity":"@e[type=minecraft:armor_stand,tag=EtiSchiff.1Richtung,sort=nearest,limit=1]","nbt":"CustomName","interpret":true},{"text":"\nneigung = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für Oben"} },"clickEvent":{"action":"run_command","value":"/trigger PZSchiff.1Wert set 5"} },{"text":"Oben","color":"gray","bold":true},{"text":"\nneigung = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für Geradeaus"} },"clickEvent":{"action":"run_command","value":"/trigger PZSchiff.1Wert set 6"} },{"text":"Geradeaus","color":"gray","bold":true},{"text":"\nneigung = ","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Tippe für Unten"} },"clickEvent":{"action":"run_command","value":"/trigger PZSchiff.1Wert set 7"} },{"text":"Unten","color":"gray","bold":true},{"text":"\nneigung == ","bold":true},{"entity":"@e[type=minecraft:armor_stand,tag=EtiSchiff.1Neigung,sort=nearest,limit=1]","nbt":"CustomName","interpret":true},{"text":"\nfahren(richtung,neigung)","color":"gray","bold":true,"hoverEvent":{"action":"show_text","value":{"text":"Schiff fahren"} },"clickEvent":{"action":"run_command","value":"/trigger PZSchiff.1Wert set 8"} }]

# Von der Position des Schiffs-Rüstungsständers erhält der Pilot die Erlaubnis den Auslöser wieder zu betätigen und sein Wert wird wieder auf null gesetzt.
execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] as @p[distance=..5,team=TMSchiff.1Kap] unless entity @s[scores={PZSchiff.1Wert=0}] run scoreboard players enable @s PZSchiff.1Wert
execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Schiff] as @p[distance=..5,team=TMSchiff.1Kap] unless entity @s[scores={PZSchiff.1Wert=0}] run scoreboard players set @s PZSchiff.1Wert 0

execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Hafen] if block ~ ~ ~ minecraft:stone_button[powered=true] run summon minecraft:item ~ ~ ~ {Item:{id:"minecraft:armor_stand",Count:1b,tag:{schiff:"v1gegenstand",EntityTag:{Small:true,NoGravity:true,Invisible:true,Tags:["EtiSchiff.1Alle","EtiSchiff.1Erstellen"] } } } }

execute as @e[type=minecraft:armor_stand,tag=EtiSchiff.1Kanal] at @s as @p if entity @s[y_rotation=135..45] unless entity @s[y_rotation=-45..45] run setblock ~ ~ ~ minecraft:structure_block{rotation:"NONE",mode:"LOAD",posX:-1,posY:0,posZ:-2,name:"schiff:v1kanal"} replace
execute as @e[type=minecraft:armor_stand,tag=EtiSchiff.1Kanal] at @s as @p if entity @s[y_rotation=-135..135] unless entity @s[y_rotation=-135..-45] run setblock ~ ~ ~ minecraft:structure_block{rotation:"CLOCKWISE_90",mode:"LOAD",posX:2,posY:0,posZ:-1,name:"schiff:v1kanal"} replace

execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Kanal] run setblock ~ ~ ~1 minecraft:redstone_block replace
kill @e[type=minecraft:armor_stand,tag=EtiSchiff.1Kanal]

execute at @e[type=minecraft:armor_stand,tag=EtiSchiff.1Erstellen] if block ^ ^-2 ^-8 minecraft:water run summon minecraft:armor_stand ^ ^2 ^-8 {Small:true,NoGravity:true,Invisible:true,CustomName:'{"text":"Westen","color":"gold","bold":true}',Tags:["EtiSchiff.1Alle","EtiSchiff.1Schiff","EtiSchiff.1Richtung"],Rotation:[90.0f,0.0f],Passengers:[{id:"minecraft:armor_stand",Small:true,NoGravity:true,Invisible:true,CustomName:'{"text":"Geradeaus","color":"aqua","bold":true}',Tags:["EtiSchiff.1Alle","EtiSchiff.1Neigung"] } ] }

kill @e[type=minecraft:armor_stand,tag=EtiSchiff.1Erstellen]
