# Es wird nach dem Feld mit der entsprechenden Position gesucht und dann wird dessen Wert in eine Variable gespeichert und anschließend in einer tellraw-Nachricht ausgegeben
execute as @e[type=minecraft:armor_stand,tag=EtiFeld.p2FeldDimension1,tag=EtiFeld.p2AlleFelder] if score @s PZFeld.p2Pos = #VarFeld.p2PositionAusgebenDimension1 PZFeld.p2Pos at @s align xz positioned ~-0.2 ~-0.5 ~-0.2 as @e[dx=64,dy=0.4,dz=0.4,type=minecraft:armor_stand,tag=EtiFeld.p2AlleFelder,tag=EtiFeld.p2FeldDimension2] if score @s PZFeld.p2Pos = #VarFeld.p2PositionAusgebenDimension2 PZFeld.p2Pos at @s align xz positioned ~-0.2 ~-0.5 ~-0.2 as @e[dx=0.4,dy=0.4,dz=64,type=minecraft:armor_stand,tag=EtiFeld.p2AlleFelder,tag=EtiFeld.p2Inhalt] if score @s PZFeld.p2Pos = #VarFeld.p2PositionAusgebenDimension3 PZFeld.p2Pos run scoreboard players operation #VarFeld.p2WertInhalt PZFeld.p2Wert = @s PZFeld.p2Wert
execute at @s run tellraw @a[distance=..25] ["",{"text":"Feld[","bold":true},{"score":{"name":"#VarFeld.p2PositionAusgebenDimension1","objective":"PZFeld.p2Pos"},"bold":true,"color":"green"},{"text":"][","bold":true},{"score":{"name":"#VarFeld.p2PositionAusgebenDimension2","objective":"PZFeld.p2Pos"},"bold":true,"color":"red"},{"text":"][","bold":true},{"score":{"name":"#VarFeld.p2PositionAusgebenDimension3","objective":"PZFeld.p2Pos"},"bold":true,"color":"blue"},{"text":"] == ","bold":true},{"score":{"name":"#VarFeld.p2WertInhalt","objective":"PZFeld.p2Wert"},"bold":true,"color":"dark_purple"} ]

# Die Position wird um eins erhöht und wenn sie kleiner gleich der maximalen Länge des Feldes ist, wird diese Funktion selbst nochmal mit der neuen Position aufgerufen
scoreboard players add #VarFeld.p2PositionAusgebenDimension3 PZFeld.p2Pos 1
execute if score #VarFeld.p2PositionAusgebenDimension3 PZFeld.p2Pos <= #VarFeld.p2Dimension3 PZFeld.p2Felder run function direktzugriffsspeicher:vp2menu_werte_dimension_3
