# Allen Rüstungsständern der 1. Dimension wird der Punktestand für die 2. Dimension um eins erhöht, um für diese die Rüstungsständer später zu spawnen
execute if entity @s[tag=EtiFeld.p2FeldAnzahlDimension2Anheben] if score #VarFeld.p2Dimension2 PZFeld.p2Felder matches ..63 run scoreboard players add @e[type=minecraft:armor_stand,name=ObjFeld.p2FeldDimension1,tag=EtiFeld.p2AlleFelder,tag=EtiFeld.p2Dimensionen] PZFeld.p2Felder 1
execute if entity @s[tag=EtiFeld.p2FeldAnzahlDimension2Anheben] if score #VarFeld.p2Dimension2 PZFeld.p2Felder matches ..63 run scoreboard players add #VarFeld.p2Dimension2 PZFeld.p2Felder 1
execute if entity @s[tag=EtiFeld.p2FeldAnzahlDimension2Anheben] if score #VarFeld.p2Dimension2 PZFeld.p2Felder matches ..63 run scoreboard players add #VarFeld.p2AktuellePositionDimension2 PZFeld.p2Pos 1

execute at @s[tag=EtiFeld.p2FeldAnzahlDimension2Anheben] run tellraw @a[distance=..25] ["Direktzugriffsspeicher.plus.2:\n",{"text":"Feld[","bold":true},{"score":{"name":"@s","objective":"PZFeld.p2Felder"},"bold":true,"color":"green"},{"text":"].Länge + 1 = neues Feld[","bold":true},{"score":{"name":"@s","objective":"PZFeld.p2Felder"},"bold":true,"color":"green"},{"text":"][","bold":true},{"score":{"name":"#VarFeld.p2Dimension2","objective":"PZFeld.p2Felder"},"bold":true,"color":"red"},{"text":"][","bold":true},{"score":{"name":"#VarFeld.p2Dimension3","objective":"PZFeld.p2Felder"},"bold":true,"color":"blue"},{"text":"]","bold":true} ]

# Die aktuelle Feldanzahl wird ausgegeben
execute at @s[tag=EtiFeld.p2FeldAnzahlDimension2Ausgeben] run tellraw @a[distance=..25] ["Direktzugriffsspeicher.plus.2:\n",{"text":"Feld[","bold":true},{"score":{"name":"#VarFeld.p2AktuellePositionDimension1","objective":"PZFeld.p2Pos"},"bold":true,"color":"green"},{"text":"].Länge = ","bold":true},{"score":{"name":"#VarFeld.p2Dimension2","objective":"PZFeld.p2Felder"},"bold":true,"color":"red"} ]

# Allen Feldern der 1. Dimension wird der Punktestand für die 2. Dimension um eins erniedrigt, solange der Wert über eins ist
execute if entity @s[tag=EtiFeld.p2FeldAnzahlDimension2Verringern] if score #VarFeld.p2Dimension2 PZFeld.p2Felder matches 1.. run scoreboard players remove @e[type=minecraft:armor_stand,name=ObjFeld.p2FeldDimension1,tag=EtiFeld.p2AlleFelder,tag=EtiFeld.p2Dimensionen] PZFeld.p2Felder 1
execute if entity @s[tag=EtiFeld.p2FeldAnzahlDimension2Verringern] if score #VarFeld.p2Dimension2 PZFeld.p2Felder matches 1.. run scoreboard players remove #VarFeld.p2Dimension2 PZFeld.p2Felder 1

execute if entity @s[tag=EtiFeld.p2FeldAnzahlDimension2Verringern] if score #VarFeld.p2AktuellePositionDimension2 PZFeld.p2Pos > #VarFeld.p2Dimension2 PZFeld.p2Felder run scoreboard players remove #VarFeld.p2AktuellePositionDimension2 PZFeld.p2Pos 1

execute at @s[tag=EtiFeld.p2FeldAnzahlDimension2Verringern] run tellraw @a[distance=..25] ["Direktzugriffsspeicher.plus.2:\n",{"text":"Feld[","bold":true},{"score":{"name":"@s","objective":"PZFeld.p2Felder"},"bold":true,"color":"green"},{"text":"].Länge - 1 = neues Feld[","bold":true},{"score":{"name":"@s","objective":"PZFeld.p2Felder"},"bold":true,"color":"green"},{"text":"][","bold":true},{"score":{"name":"#VarFeld.p2Dimension2","objective":"PZFeld.p2Felder"},"bold":true,"color":"red"},{"text":"][","bold":true},{"score":{"name":"#VarFeld.p2Dimension3","objective":"PZFeld.p2Felder"},"bold":true,"color":"blue"},{"text":"]","bold":true} ]
