# Die Güterlore wird dann vom Menü wieder zurück gesetzt und bekommt alle Etiketten gelöscht, in dem die Etikette neu gesetzt werden
execute as @e[type=minecraft:chest_minecart,tag=EtiFeld.p1Start,tag=EtiFeld.p1AlleFelder,sort=nearest,limit=1] unless entity @s[scores={PZFeld.p1Wert=14}] run data merge entity @s {Tags:["EtiFeld.p1Start","EtiFeld.p1AlleFelder"],Items:[{Slot:2b,id:"minecraft:lime_concrete_powder",Count:1b,tag:{display:{Name:'{"text":"Anzahl erhöhen (1. D)"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:3b,id:"minecraft:lime_concrete_powder",Count:1b,tag:{display:{Name:'{"text":"Anzahl erhöhen (2. D)"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:4b,id:"minecraft:lime_concrete_powder",Count:1b,tag:{display:{Name:'{"text":"Zur höheren Position"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:5b,id:"minecraft:lime_concrete_powder",Count:1b,tag:{display:{Name:'{"text":"Wert erhöhen"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:10b,id:"minecraft:lime_shulker_box",Count:1b,tag:{display:{Name:'{"text":"Felder löschen und neu erzeugen"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:11b,id:"minecraft:red_shulker_box",Count:1b,tag:{display:{Name:'{"text":"Aktuelle Feldanzahl ausgeben (1. D)"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:12b,id:"minecraft:chest",Count:1b,tag:{display:{Name:'{"text":"Aktuelle Feldanzahl ausgeben (2. D)"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:13b,id:"minecraft:daylight_detector",Count:1b,tag:{display:{Name:'{"text":"Aktuelle Position ausgeben"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:14b,id:"minecraft:book",Count:1b,tag:{display:{Name:'{"text":"Aktuellen Wert ausgeben"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:15b,id:"minecraft:bookshelf",Count:1b,tag:{display:{Name:'{"text":"Werte aller Felder ausgeben"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:20b,id:"minecraft:red_concrete_powder",Count:1b,tag:{display:{Name:'{"text":"Anzahl verringern (1. D)"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:21b,id:"minecraft:red_concrete_powder",Count:1b,tag:{display:{Name:'{"text":"Anzahl verringern (2. D)"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:22b,id:"minecraft:red_concrete_powder",Count:1b,tag:{display:{Name:'{"text":"Zur niedrigeren Position"}',Lore:['"Klicke auf den Gegenstand"']} } },{Slot:23b,id:"minecraft:red_concrete_powder",Count:1b,tag:{display:{Name:'{"text":"Wert verringern"}',Lore:['"Klicke auf den Gegenstand"']} } } ]}

# Falls die Option Felder erzeugen angeklickt wurde, besitzt die Variable einen Wert ab eins oder höher, wenn dass der Fall ist, wird der Güterlore der Gegenstand fürs "Feld hinzufügen" aktiviert
execute if score #VarFeld.p1FelderErzeugen PZFeld.p1Felder matches 1.. run data remove entity @e[type=minecraft:chest_minecart,tag=EtiFeld.p1Start,tag=EtiFeld.p1AlleFelder,sort=nearest,limit=1] Items[0]

# Die Anzahl der belegten Slots, in der schwebende Güterlore mit Shulkerkiste, wird ausgelesen und nur wenn sie nicht übereinstimmt, wird die Funktion geladen
execute as @e[type=minecraft:chest_minecart,tag=EtiFeld.p1Start,tag=EtiFeld.p1AlleFelder,sort=nearest,limit=1] store result score @s PZFeld.p1Wert run data get entity @s Items
execute as @e[type=minecraft:chest_minecart,tag=EtiFeld.p1Start,tag=EtiFeld.p1AlleFelder,sort=nearest,limit=1] unless entity @s[scores={PZFeld.p1Wert=14}] run function direktzugriffsspeicher:vp1menu

# Von der Güterlore aus, werden in einem Radius von maximal 128, alle Felder der ersten Dimension ausgewählt und geprüft ob sie noch Felder beantragen, falls ja wird die Funktion für die zweite Dimension aufgerufen
execute at @e[type=minecraft:chest_minecart,tag=EtiFeld.p1Start,tag=EtiFeld.p1AlleFelder,sort=nearest,limit=1] as @e[distance=..128,sort=nearest,type=minecraft:armor_stand,tag=EtiFeld.p1FeldDimension1,tag=EtiFeld.p1AlleFelder,tag=EtiFeld.p1Dimensionen] unless score @s PZFeld.p1Felder matches 0 at @s run function direktzugriffsspeicher:vp1dimension_2

# Alle Felder bekommen den Leuchteffekt entfernt
effect clear @e[tag=EtiFeld.p1Dimensionen] minecraft:glowing

# Das ausgewählte Feld der ersten Dimension erhält Leucht- und Partikeleffekte, um es sofort ausfindig machen zu können
execute as @e[type=minecraft:armor_stand,tag=EtiFeld.p1FeldDimension1,tag=EtiFeld.p1AlleFelder] if score @s PZFeld.p1Pos = #VarFeld.p1AktuellePositionDimension1 PZFeld.p1Pos run effect give @s minecraft:glowing 1 1
execute as @e[type=minecraft:armor_stand,tag=EtiFeld.p1FeldDimension1,tag=EtiFeld.p1AlleFelder] if score @s PZFeld.p1Pos = #VarFeld.p1AktuellePositionDimension1 PZFeld.p1Pos at @s run particle minecraft:block minecraft:lime_concrete ~ ~1.1 ~ 0 0 0 0.1 1 normal @p[distance=..25]

# Das ausgewählte Feld der zweiten Dimension erhält Leucht- und Partikeleffekte, um es sofort ausfindig machen zu können
execute as @e[type=minecraft:armor_stand,tag=EtiFeld.p1FeldDimension1,tag=EtiFeld.p1AlleFelder] if score @s PZFeld.p1Pos = #VarFeld.p1AktuellePositionDimension1 PZFeld.p1Pos at @s align xyz positioned ~-0.2 ~-0.5 ~-0.2 as @e[dx=64,dy=0.4,dz=0.4,type=minecraft:armor_stand,tag=EtiFeld.p1Inhalt,tag=EtiFeld.p1AlleFelder] if score @s PZFeld.p1Pos = #VarFeld.p1AktuellePositionDimension2 PZFeld.p1Pos run effect give @s minecraft:glowing 1 1
execute as @e[type=minecraft:armor_stand,tag=EtiFeld.p1FeldDimension1,tag=EtiFeld.p1AlleFelder] if score @s PZFeld.p1Pos = #VarFeld.p1AktuellePositionDimension1 PZFeld.p1Pos at @s align xyz positioned ~-0.2 ~-0.5 ~-0.2 as @e[dx=64,dy=0.4,dz=0.4,type=minecraft:armor_stand,tag=EtiFeld.p1Inhalt,tag=EtiFeld.p1AlleFelder] if score @s PZFeld.p1Pos = #VarFeld.p1AktuellePositionDimension2 PZFeld.p1Pos at @s run particle minecraft:block minecraft:red_concrete ~ ~1.1 ~ 0 0 0 0.1 1 normal @p[distance=..25]
