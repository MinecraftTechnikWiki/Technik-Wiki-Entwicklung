# Die Güterlore wird dann vom Menü wieder zurück gesetzt und bekommt alle Etiketten gelöscht, in dem die Etikette neu gesetzt werden
execute as @e[type=minecraft:chest_minecart,tag=EtiFeld.1Start,tag=EtiFeld.1AlleFelder,sort=nearest,limit=1] unless entity @s[scores={PZFeld.1Wert=11}] run data merge entity @s {Tags:["EtiFeld.1Start","EtiFeld.1AlleFelder"],Items:[{Slot:3b,id:"minecraft:lime_concrete_powder",Count:1b,tag:{display:{Name:"{\"text\":\"Anzahl erhöhen\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:4b,id: "minecraft:lime_concrete_powder",Count:1b,tag:{display:{Name:"{\"text\":\"Zur höheren Position\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:5b,id:"minecraft:lime_concrete_powder",Count:1b,tag:{display:{Name:"{\"text\":\"Wert erhöhen\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:11b,id:"minecraft:lime_shulker_box",Count:1b,tag:{display:{Name:"{\"text\":\"Felder löschen und neu erzeugen\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:12b,id:"minecraft:chest",Count:1b, tag:{display:{Name:"{\"text\":\"Aktuelle Feldanzahl ausgeben\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:13b,id:"minecraft:daylight_detector",Count:1b,tag:{display:{Name:"{\"text\":\"Aktuelle Position ausgeben\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:14b,id:"minecraft:book",Count:1b,tag:{display:{Name: "{\"text\":\"Aktuellen Wert ausgeben\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:15b,id:"minecraft:bookshelf",Count:1b,tag:{display:{Name: "{\"text\":\"Werte aller Felder ausgeben\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:21b,id:"minecraft:red_concrete_powder",Count:1b,tag:{display:{Name:"{\"text\":\"Anzahl verringern\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:22b,id:"minecraft:red_concrete_powder",Count:1b,tag:{display:{Name:"{\"text\":\"Zur niedrigeren Position\"}",Lore:["Klicke auf den Gegenstand"]} } },{Slot:23b,id:"minecraft:red_concrete_powder",Count:1b,tag:{display:{Name:"{\"text\":\"Wert verringern\"}",Lore:["Klicke auf den Gegenstand"]} } } ]}

# Falls die Option Felder erzeugen angeklickt wurde, besitzt die Variable einen Wert ab eins oder höher, wenn dass der Fall ist, wird der Güterlore der Gegenstand fürs "Feld hinzufügen" aktiviert
execute if score #VarFeld.1FelderErzeugen PZFeld.1Wert matches 1.. run data remove entity @e[type=minecraft:chest_minecart,tag=EtiFeld.1Start,tag=EtiFeld.1AlleFelder,sort=nearest,limit=1] Items[0]

# Die Anzahl der belegten Slots, in der schwebende Güterlore mit Shulkerkiste, wird ausgelesen und nur wenn sie nicht übereinstimmt, wird die Funktion geladen
execute as @e[type=minecraft:chest_minecart,tag=EtiFeld.1Start,tag=EtiFeld.1AlleFelder,sort=nearest,limit=1] store result score @s PZFeld.1Wert run data get entity @s Items
execute as @e[type=minecraft:chest_minecart,tag=EtiFeld.1Start,tag=EtiFeld.1AlleFelder,sort=nearest,limit=1] unless entity @s[scores={PZFeld.1Wert=11}] run function direktzugriffsspeicher:v1menu

# Das ausgewählte Feld erhält Leucht- und Partikeleffekte, um es sofort ausfindig machen zu können
execute as @e[type=minecraft:armor_stand,name=ObjFeld.1Inhalt,tag=EtiFeld.1AlleFelder] unless score @s PZFeld.1Pos = #VarFeld.1AktuellePosition PZFeld.1Pos run effect clear @s minecraft:glowing
execute as @e[type=minecraft:armor_stand,name=ObjFeld.1Inhalt,tag=EtiFeld.1AlleFelder] if score @s PZFeld.1Pos = #VarFeld.1AktuellePosition PZFeld.1Pos run effect give @s minecraft:glowing 1 1
execute as @e[type=minecraft:armor_stand,name=ObjFeld.1Inhalt,tag=EtiFeld.1AlleFelder] if score @s PZFeld.1Pos = #VarFeld.1AktuellePosition PZFeld.1Pos at @s run particle minecraft:block minecraft:lime_concrete ~ ~1.1 ~ 0 0 0 0.1 1 normal @p[distance=..25]
